set %containerId NHOAKMD
set %wrestling #true
set %spiritSpeakHeal #true
set %weaponType VOH ; TSF = dagger (fencing) CNF = skinning knife (swordsmanship) VOH = club (macefighting)
set %kickOutHits 30

set %alligator UF
set %grizzlyBear WF
set %creatureTypes %alligator , _ , %grizzlyBear

set %waterTrough VEE_QEE
set %bloodyBandages AMF
set %spiderSilk RZF
set %bloodMoss JUF
set %mandrakeRoot MZF
set %bandages ZLF
set %shields GIK_NIK_CIK

msg $,waitmenu 'What do you want to summon ?' 'Crocodile'$
msg $,automenu 'What do you want to summon ?' 'Crocodile'$

if ( %spiritSpeakHeal = #false )
{
    gosub openContainer %containerId
    findItem %bandages C_ , #backPackId
    if ( #findKind = -1 || #findStack < 15 )
        gosub restockFromContainer %containerId %bandages 15
}

loop:
gosub openContainer %containerId
if ( %wrestling = #false )
{
    findItem %weaponType C_ , #backPackId
    if ( #findKind = -1 )
        gosub restockFromContainer %containerId %weaponType 1
}
findItem %shields C_ , #charId
if ( #findKind = -1 )
    gosub restockFromContainer %containerId %shields 1
findItem %spiderSilk C_ , #backPackId
if ( #findKind = -1 )
    gosub restockFromContainer %containerId %spiderSilk 10
findItem %bloodMoss C_ , #backPackId
if ( #findKind = -1 )
    gosub restockFromContainer %containerId %bloodMoss 10
findItem %mandrakeRoot C_ , #backPackId
if ( #findKind = -1 )
    gosub restockFromContainer %containerId %mandrakeRoot 10
event macro 6
if ( %spiritSpeakHeal = #false )
{
    gosub cleanBandages
    gosub healBandages
}
if ( %spiritSpeakHeal = #true )
    gosub healSpiritSpeak
gosub meditate
gosub summon
if ( %wrestling = #false )
    gosub equip %weaponType
gosub equip %shields
gosub attack
goto loop

sub cleanBandages
findItem %bloodyBandages C_ , #backPackId
if ( #findKind = -1 )
    return
set #lObjectId #findId
findItem %waterTrough G_2
if ( #findKind = -1 )
    return
set #lTargetId #findId
set #lTargetKind 1
event macro 17
target 6s
event macro 22
return

; gosub openContainer [containerId]
sub openContainer
set %_containerId %1
_openContainer:
set #lObjectId %_containerId
event macro 17
set %_timeout #scnt + 10
_waitOpenContainer:
if ( %_timeout < #scnt )
    goto _openContainer
if ( #contId = %_containerId )
   return
goto _openContainer

sub restockFromContainer
set %_grabFromContainerContainerId %1
set %_grabFromContainerItemId %2
set %_grabFromContainerAmount %3
findItem %_grabFromContainerItemId C_ , %_grabFromContainerContainerId
if ( #findKind = -1 || #findStack < %_grabFromContainerAmount )
    return #false
exevent drag #findId %_grabFromContainerAmount
exevent dropC #backpackId
wait 10
return #true

sub healBandages
_healBandages:
while ( #hits < 100 )
{
    findItem %bandages C_ , #backPackId
    if ( #findKind = -1 )
        goto _healBandages
    set #lObjectId #findId
    event macro 17
    target 6s
    event macro 23
    set %_bandagingTimeout #scnt + 15
    set %_bandagingJournal #jIndex
    _bandaging:
    if ( %_bandagingTimeout < #scnt )
        goto _bandagingDone
    if ( #jIndex < %_bandagingJournal )
        goto _bandaging
    scanJournal %_bandagingJournal
    if ( you_fail in #journal )
        goto _bandagingDone
    if ( you_finish_applying in #journal )
        goto _bandagingDone
    set %_bandagingJournal %_bandagingJournal + 1
    goto _bandaging
    _bandagingDone:
}
return

sub healSpiritSpeak
_healSpiritSpeak:
if ( #hits >= 100 )
    return
if ( #mana < 100 )
    gosub meditate
event macro 13 32
set %_spiritSpeakTimeout #scnt + 5
set %_spiritSpeakJournal #jIndex
_spiritSpeak:
if ( %_spiritSpeakTimeout < #scnt )
    goto _healSpiritSpeak
if ( %_spiritSpeakJournal > #jIndex )
    goto _spiritSpeak
scanJournal %_spiritSpeakJournal
if ( you_fail_your_attempt_at_contacting in #journal )
    goto _healSpiritSpeak
if ( you_channel_your_own_spiritual in #journal )
    goto _healSpiritSpeak
if ( you_are_preoccupied_with_thoughts_of_battle in #journal )
    event macro 6
set %_spiritSpeakJournal %_spiritSpeakJournal + 1
goto _spiritSpeak

sub summon
_summon:
findItem %spiderSilk , _ , %bloodMoss , _ , %mandrakeRoot C_ , #backPackId
if ( #findKind = -1 || #findCnt <> 3 )
    return
event macro 15 39
target 6s
event macro 23
set %_summonTimeout #scnt + 10
_summonTimeout:
if ( %_summonTimeout < #scnt )
    goto _summon
findItem %creatureTypes G_1
if ( #findKind = -1 )
    goto _summonTimeout
return

sub equip
set %_equipId %1
findItem %_equipId C_ , #backPackId
if ( #findKind = -1 )
    return #false
exevent drag #findId 1
exevent dropPd
wait 10
return #true

sub attack
set %_attack #scnt
_attack:
findItem %creatureTypes G_1
if ( #findKind = -1 )
    return
if ( #hits <= %kickOutHits )
{
    set #lTargetId #findId
    set #lTargetKind 1
    msg $kick out$
    target 6s
    event macro 22
    goto _attack
}
if ( %_attack <= #scnt )
{
    set #lTargetId #findId
    event macro 27
    set %_attack #scnt + 3
}
goto _attack

sub meditate
_meditate:
if ( #mana >= 100 )
    return
set %_meditateJournal #jIndex
event macro 13 46
_meditationJournal:
if ( #mana >= 100 )
    return
if ( #jIndex <= %_meditateJournal )
    goto _meditationJournal
set %_meditateJournal %_meditateJournal + 1
scanJournal %_meditateJournal
if ( you_are_at_peace in #journal )
    return
if ( you_lose_your_concentration in #journal )
    goto _meditate
goto _meditationJournal
