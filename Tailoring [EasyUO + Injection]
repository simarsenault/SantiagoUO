set %containerId OHOAKMD
set %trashCanId CNYZJMD

set %sewingKit HAG
set %foldedCloth RTI
set %bandana TDI

msg $,automenu 'Cloth' 'Headwear'$
msg $,automenu 'Headwear' 'bandana'$

loop:
findItem %foldedCloth C_ , #backPackId
if ( #findKind = -1 || #findStack < 100 )
{
    gosub trashBandanas
    gosub openContainer %containerId
    gosub recharge
}
findItem %sewingKit C_ , #backPackId
if ( #findKind = -1 )
    pause
set #lObjectId #findId
findItem %foldedCloth C_ , #backPackId
set #lTargetId #findId
set #lTargetKind 1
event macro 17
target
event macro 22
wait 80
goto loop

sub trashBandanas
set %_trashedBandanas 0
_trashBandanas:
findItem %bandana C_ , #backPackId
if ( #findKind = -1 )
    return
exevent drag #findId 1
exevent dropC %trashCanId
wait 10
set %_trashedBandanas %_trashedBandanas + 1
if ( %_trashedBandanas >= 10 )
    goto _emptyTrash
goto _trashBandanas
_emptyTrash:
set #lTargetId %trashCanId
set #lTargetKind 1
msg $take out the trash$
target
event macro 22
set %_trashedBandanas 0
goto _trashBandanas

sub recharge
findItem %foldedCloth C_ , %containerId
if ( #findKind = -1 || #findStack < 300 )
    pause
exevent drag #findId 300
exevent dropC #backPackId
wait 10
return

; gosub openContainer [containerId]
sub openContainer
set %_containerId %1
_openContainer:
set #lObjectId %_containerId
event macro 17
set %_timeout #scnt + 10
_waitOpenContainer:
if ( %_timeout < #scnt )
    goto _openContainer
if ( #contId = %_containerId )
   return
goto _openContainer
