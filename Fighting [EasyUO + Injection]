set %containerId NHOAKMD
set %weaponType TSF ; TSF = dagger
set %creatureType UF

set %waterTrough VEE_QEE
set %bloodyBandages AMF
set %spiderSilk RZF
set %bloodMoss JUF
set %mandrakeRoot MZF
set %bandages ZLF

msg $,automenu 'What do you want to summon ?' 'Crocodile'$
gosub openContainer %containerId
gosub restockFromContainer %containerId %bandages 10
gosub restockFromContainer %containerId %weaponType 1

loop:
gosub openContainer %containerId
gosub restockFromContainer %containerId %spiderSilk 1
gosub restockFromContainer %containerId %bloodMoss 1
gosub restockFromContainer %containerId %mandrakeRoot 1
gosub cleanBandages
gosub heal
gosub summon
gosub equipWeapon
gosub attack
goto loop

sub cleanBandages
findItem %bloodyBandages C_ , #backPackId
if ( #findKind = -1 )
    return
set #lObjectId #findId
findItem %waterTrough G_2
if ( #findKind = -1 )
    return
set #lTargetId #findId
set #lTargetKind 1
event macro 17
target 6s
event macro 22
return

; gosub openContainer [containerId]
sub openContainer
set %_containerId %1
_openContainer:
set #lObjectId %_containerId
event macro 17
set %_timeout #scnt + 10
_waitOpenContainer:
if ( %_timeout < #scnt )
    goto _openContainer
if ( #contId = %_containerId )
   return
goto _openContainer

sub restockFromContainer
set %_grabFromContainerContainerId %1
set %_grabFromContainerItemId %2
set %_grabFromContainerAmount %3
findItem %_grabFromContainerItemId C_ , %_grabFromContainerContainerId
if ( #findKind = -1 || #findStack < %_grabFromContainerAmount )
    return #false
exevent drag #findId %_grabFromContainerAmount
exevent dropC #backpackId
wait 10
return #true

sub heal
_heal:
while ( #hits < 100 )
{
    findItem %bandages C_ , #backPackId
    if ( #findKind = -1 )
        goto _heal
    set #lObjectId #findId
    event macro 17
    target 6s
    event macro 23
    set %_bandagingTimeout #scnt + 15
    set %_bandagingJournal #jIndex
    _bandaging:
    if ( %_bandagingTimeout < #scnt )
        goto _bandagingDone
    if ( #jIndex < %_bandagingJournal )
        goto _bandaging
    scanJournal %_bandagingJournal
    if ( you_fail in #journal )
        goto _bandagingDone
    if ( you_finish_applying in #journal )
        goto _bandagingDone
    set %_bandagingJournal %_bandagingJournal + 1
    goto _bandaging
    _bandagingDone:
}
return

sub summon
_summon:
findItem %spiderSilk , _ , %bloodMoss , _ , %mandrakeRoot C_ , #backPackId
if ( #findKind = -1 || #findCnt <> 3 )
    return
event macro 15 39
target 6s
event macro 23
set %_summonTimeout #scnt + 10
_summonTimeout:
if ( %_summonTimeout < #scnt )
    goto _summon
findItem %creatureType G_1
if ( #findKind = -1 )
    goto _summonTimeout
return

sub equipWeapon
findItem %weaponType C_ , #backPackId
if ( #findKind = -1 )
    return #false
exevent drag #findId 1
exevent dropPd
wait 10
return #true

sub attack
set %_attack #scnt
_attack:
findItem %creatureType G_1
if ( #findKind = -1 )
    return
if ( %_attack <= #scnt )
{
    set #lTargetId #findId
    event macro 27
    set %_attack #scnt + 3
}
goto _attack
