set %containerId OHOAKMD
set %craft Flamestrike           ; Recall | Flamestrike
set %recharge #false

if ( %craft = Recall )
{
    msg $,automenu 'Spell Circles' 'Fourth Circle'$
    msg $,automenu 'Spell Circle 4' 'Recall'$
}
if ( %craft = Flamestrike )
{
    msg $,automenu 'Spell Circles' 'Seventh Circle'$
    msg $,automenu 'Spell Circle 7' 'Flamestrike'$
}
wait 20

loop:
findItem MMF C_ , #backPackId
if ( #findKind = -1 || #findStack < 10 )
    set %recharge #true
if ( %craft = Recall )
{
    findItem KUF C , #backPackId
    if ( #findKind = -1 )
        set %recharge #true
    findItem JUF C , #backPackId
    if ( #findKind = -1 )
        set %recharge #true
    findItem MZF C , #backPackId
    if ( #findKind = -1 )
        set %recharge #true
}
if ( %craft = Flamestrike )
{
    findItem SZF C , #backPackId
    if ( #findKind = -1 || #findStack < 10 )
        set %recharge #true
    findItem RZF C , #backPackId
    if ( #findKind = -1 || #findStack < 10 )
        set %recharge #true
}
if ( %recharge = #true )
{
    gosub recharge
    set %recharge #false
}
event macro 13 23
wait 80
goto loop

sub recharge
gosub backpackToContainer MMF %containerId
if ( %craft = Recall )
{
    gosub backpackToContainer KUF %containerId
    gosub backpackToContainer JUF %containerId
    gosub backpackToContainer MZF %containerId
    gosub backpackToContainer WTL %containerId
}
if ( %craft = Flamestrike )
{
    gosub backpackToContainer SZF %containerId
    gosub backpackToContainer RZF %containerId
    gosub backpackToContainer NUL %containerId
}
gosub openContainer %containerId
gosub containerToBackpack MMF 100 %containerId
if ( #result = #false )
    pause
if ( %craft = Recall )
{
    gosub containerToBackpack KUF 100 %containerId
    if ( #result = #false )
        pause
    gosub containerToBackpack JUF 100 %containerId
    if ( #result = #false )
        pause
    gosub containerToBackpack MZF 100 %containerId
    if ( #result = #false )
        pause
}
if ( %craft = Flamestrike )
{
    gosub containerToBackpack SZF 100 %containerId
    if ( #result = #false )
        pause
    gosub containerToBackpack RZF 100 %containerId
    if ( #result = #false )
        pause
}
return

sub backpackToContainer
set %_backpackToContainerFindId %1
set %_backpackToContainerContainerId %2
findItem %_backpackToContainerFindId C_ , #backPackId
if ( #findKind = -1 )
    return
exevent drag #findId #findStack
wait 10
exevent dropC %_backpackToContainerContainerId
wait 10
return

sub containerToBackpack
set %_containerToBackpackFindId %1
set %_containerToBackpackAmount %2
set %_containerToBackpackContainerId %3
findItem %_containerToBackpackFindId C_ , %_containerToBackpackContainerId
if ( #findKind = -1 || #findStack < %_containerToBackpackAmount )
    return #false
exevent drag #findId %_containerToBackpackAmount
wait 10
exevent dropC #backPackId
wait 10
return #true

; gosub openContainer [containerId]
sub openContainer
set %_containerId %1
_openContainer:
set #lObjectId %_containerId
event macro 17
set %_timeout #scnt + 10
_waitOpenContainer:
if ( %_timeout < #scnt )
    goto _openContainer
if ( #contId = %_containerId )
   return
goto _openContainer
