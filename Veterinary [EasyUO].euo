; Make sure you have bandages in your backpack
; Need water trough on ground within 1 tile to clean bandages

set %petOne NTBI          ; Has to be named "One"
set %petTwo YFII          ; Has to be named "Two"
set %petThree YWHI        ; Has to be named "Three"

set %waterTrough VEE_QEE
set %bloodyBandages AMF
set %bandages ZLF

ignoreItem reset

set %attackTimer #scnt

loop:
if ( %attackTimer <= #scnt )
{
    gosub attack One %petTwo
    gosub attack Two %petThree
    gosub attack Three %petOne
    set %attackTimer #scnt + 3
}
gosub cleanBandages
gosub heal %petOne
gosub heal %petTwo
gosub heal %petThree
goto loop

sub cleanBandages
findItem %bloodyBandages C_ , #backPackId
if ( #findKind = -1 )
    return
set #lObjectId #findId
findItem %waterTrough G_2
if ( #findKind = -1 )
    return
set #lTargetId #findId
set #lTargetKind 1
event macro 17
target 6s
event macro 22
return

sub attack
set %_attackName %1
set %_attackId %2
set #lTargetId %_attackId
set #lTargetKind 1
msg $ , %_attackName kill$
target 6s
event macro 22
return

sub heal
set %_healId %1
_heal:
findItem ZLF C_ , #backPackId
set #lObjectId #findId
set #lTargetId %_healId
set #lTargetKind 1
event macro 17
target 6s
set %_healJournal #jIndex + 1
set %_healTimeout #scnt + 3
event macro 22
_healJournal:
if ( %_healTimeout < #scnt )
    return
if ( %_healJournal > #jIndex  )
    goto _healJournal
scanJournal %_healJournal
if ( it_is_not_wounded in #journal )
    return
if ( you_finish_applying_bandages in #journal )
    return
if ( you_fail_to_apply in #journal )
    goto _heal
set %_healJournal %_healJournal + 1
goto _healJournal
